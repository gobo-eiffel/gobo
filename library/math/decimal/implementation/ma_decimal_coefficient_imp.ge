indexing

	description: 
	
		"Simple implementation of coefficients using a native array of characters"

	library: "Gobo Eiffel Decimal Arithmetic Library"
	copyright: "Copyright (c) 2004, Paul G. Crismer and others"
	license: "Eiffel Forum License v2 (see forum.txt)"
	date: "$Date$"

class MA_DECIMAL_COEFFICIENT_IMP

inherit

	MA_DECIMAL_COEFFICIENT
		redefine
			out, three_way_comparison
		end

	KL_INTEGER_ROUTINES
		export
			{NONE} all
		undefine
			out, copy, is_equal
		end

creation

	make, make_copy

feature {NONE} -- Initialization

	make (a_capacity: INTEGER) is
			-- Make with `a_capacity'.
		do
			digits := SPECIAL_DIGITS_.make (a_capacity)
			capacity := a_capacity
			set_count (0)
		end

	make_copy (other: like Current) is
			-- Make a copy of `other'.
		do
			make (other.capacity)
			copy (other)
		end

feature -- Access

	item (index: INTEGER): INTEGER is
			-- Item at `index'
		do
#ifdef VE
		Result := digits.item (index).code
#else
		Result := digits.item (index)
#endif
		end

	msd_index: INTEGER is
			-- Index of most significant (non-zero) digit
		local
			l_digits: like digits
		do
			Result := count
			from
				l_digits := digits
			until
#ifdef VE
				Result = 0 or else l_digits.item (Result - 1) /= '%U' 
#else
				Result = 0 or else l_digits.item (Result - 1) /= 0
#endif
			loop
				Result := Result - 1
			end
			if Result > 0 then
				Result := Result - 1
			end
		end

	subcoefficient (index_start, index_end : INTEGER) : MA_DECIMAL_COEFFICIENT is
			-- subcoefficient made of digits in range [index_start..index_end]
		local
			index : INTEGER
		do
			create {MA_DECIMAL_COEFFICIENT_IMP} Result.make (index_end - index_start + 1)
			from
				index := index_start
			until
				index > index_end
			loop
				Result.put (item (index), Result.count)
				index := index + 1
			end
		end

feature -- Measurement

	count: INTEGER
			-- Number of decimal digits

	lower: INTEGER is
			-- Lower index
		do
			Result := 0 --digits.lower
		end

	upper: INTEGER is
			-- Upper index
		do
			Result := capacity - 1
		end

	capacity: INTEGER
			-- Capacity

feature -- Element change

	set_from_substring (s: STRING; coefficient_begin, coefficient_end: INTEGER) is
			-- Set from `s', skip the decimal point if it is present.
		local
			i, k: INTEGER
			c: CHARACTER
			l_digits: like digits
		do
			if s.count > capacity then
				grow (s.count)
			end
			l_digits := digits
			from
				i := coefficient_end
				k := 0
			variant
				i
			until
				i < coefficient_begin
			loop
				c := s.item (i)
				inspect c
				when '0'..'9' then
#ifdef VE
					l_digits.put (to_character ((c.code - ('0').code)), k) 
#else
					put ((c.code - ('0').code).to_integer_8, k)
#endif
					k := k+1
				else
					-- Do nothing.
				end
				i := i - 1
			end
			set_count (k)
		end

	grow (a_capacity: INTEGER) is
			-- Grow coefficient so that it can contain up to `a_capacity' digits.
		local
			index, new_upper: INTEGER
			l_digits: like digits
		do
			new_upper := a_capacity - 1
			if a_capacity > capacity then
				digits := SPECIAL_DIGITS_.resize (digits, new_upper + 1) --; old_size: INTEGER; new_size: INTEGER) digits.resize (0, new_upper)
				capacity := new_upper + 1
			end
				-- Zero msd.
			from
				index := count
				l_digits := digits
			until
				index > new_upper
			loop
#ifdef VE
				l_digits.put ('%U', index) 
#else
				put (0, index)
#endif
				index := index + 1
			end
			set_count (a_capacity)
		end

	put (v, index: INTEGER) is
			-- Put `v' at `index'-th item.
		do
#ifdef VE
			digits.put (to_character (v), index)
#else
			digits.put (v.to_integer_8, index)
#endif
			if index > count - 1 then
				set_count (index + 1)
			end
		end

feature -- Comparison

	three_way_comparison (other: like Current): INTEGER is
			-- Compare `other'; Result is [-1,0,+1] if [Current < other, Current = other, Current > other] respectively
		local
			index, count_a, count_b, local_difference: INTEGER
			l_digits, l_other_digits: like digits
		do
				-- Skip leading zeroes.
			count_a := msd_index + 1
			count_b := other.msd_index + 1

			if count_a > count_b then
				Result := 1
			elseif count_a < count_b then
				Result := -1
			else
				from
					index := count_a - 1
					l_digits := digits
					l_other_digits := other.digits
				until
					index < 0 or else local_difference /= 0
				loop
#ifdef VE
					local_difference := l_digits.item (index).code - l_other_digits.item (index).code 
#else					
					local_difference := l_digits.item (index) - l_other_digits.item (index)
#endif
					index := index - 1
				end
				Result := local_difference.sign
			end
		end

feature -- Conversion

	out: STRING is
			-- Terse printable representation
		local
			index : INTEGER
		do
			create Result.make (count)
			from
				index := count - 1
			until
				index < 0
			loop
				Result.append_string (item (index).out)
				index := index - 1
			end
		end

feature -- Duplication

	copy (other: like Current) is
			-- copy `other' into `Current' without aliasing.
		local
			index, l_upper: INTEGER
			l_digits, l_other_digits: like digits
		do
			if Current /= other then
				l_upper := other.count -1
				if digits = Void then
					make (l_upper + 1)
				elseif capacity < other.capacity then
					grow (other.capacity)
				end
				from
					index := 0
					l_digits := digits
					l_other_digits := other.digits
				until
					index > l_upper
				loop
					l_digits.put (l_other_digits.item (index), index) --put (other.item (index), index)
					index := index + 1
				end
				set_count (index)
			end
		end

	to_twin: like Current is
			-- Cloned version of `Current'
		do
			create Result.make_copy (Current)
		end

feature -- Basic operations

	keep_head (a_count: INTEGER) is
			-- Keep head of 'a_count' digits.
		local
			index: INTEGER
			l_digits: like digits
		do
				-- Initialize "tail" [a_count..upper] to zero.
				-- Must do it before adapting `count' since `put' can modify `count'.
			from
				index := a_count
				l_digits := digits
			until
				index > upper
			loop
#ifdef VE
				l_digits.put ('%U', index) 
#else
				l_digits.put (0, index)
#endif
				index := index + 1
			end
				-- Keep values [0..a_count-1].
			set_count (a_count)
		end

	is_equal (other: like Current): BOOLEAN is
			-- Are `Current' and `other' considered equal?
		local
			index: INTEGER
			l_digits, l_other_digits: like digits
		do
			if other.count = count then
				from
					index := count - 1
					l_digits := digits
					l_other_digits := other.digits
				variant
					index + 1
				until
#ifdef VE
					index < lower or else l_digits.item (index).code /= l_other_digits.item (index).code
#else
					index < lower or else l_digits.item (index) /= l_other_digits.item (index)
#endif
				loop
					index := index - 1
				end
				Result := (index < lower) -- found no equal item
			end
		end

	shift_left (a_count: INTEGER) is
			-- Shift items left.
		local
			index: INTEGER
			l_digits: like digits
		do
			grow (count + a_count)
				-- Copy digits `a_count' positions left.
			from
				index := count - 1
				l_digits := digits
			until
				index < a_count
			loop
				l_digits.put (l_digits.item (index - a_count), index) --put (item (index - a_count), index)
				index := index - 1
			end
				-- Set zeroes as lsb.
			from
			until
				index < 0
			loop
#ifdef VE
				l_digits.put ('%U', index)
#else
				l_digits.put (0, index)
#endif
				index := index - 1
			end
		end

	shift_right (a_count: INTEGER) is
			-- Shift items right.
		local
			index: INTEGER
			l_digits: like digits
		do
				-- Copy digits `a_count' positions right
				-- i.e copy suffix [a_count..count] right.
			from
				index := a_count
				l_digits := digits
			until
				index >= count
			loop
				l_digits.put (l_digits.item (index), index - a_count) --put (item (index), index - a_count)
				index := index + 1
			end
				-- Set zeroes as msb.
			from
				index := count - a_count
			until
				index >= count
			loop
#ifdef VE
				l_digits.put ('%U', index) 
#else
				l_digits.put (0, index)
#endif
				index := index + 1
			end
		end

	integer_add (other: like Current) is
			-- Integer add of `other' to `Current'.
		local
			carry: INTEGER
			index: INTEGER
			digit: INTEGER
			l_digits, l_other_digits: like digits
		do
			from
				carry := 0
				index := 0
				l_digits := digits
				l_other_digits := other.digits
			until
				index = count
			loop
#ifdef VE
				carry := carry + l_digits.item (index).code + l_other_digits.item (index).code 
#else
				carry := carry +  l_digits.item (index) + l_other_digits.item (index)
#endif
				digit := carry \\ 10
#ifdef VE
				l_digits.put (to_character (digit), index) 
#else
				l_digits.put (digit.to_integer_8, index)
#endif
				carry := carry // 10
				index := index + 1
			end
			if carry /= 0 then
				grow (count + 1)
#ifdef VE
				l_digits.put (to_character (carry), index)
#else
				l_digits.put (carry.to_integer_8, index)
#endif
			end
		end

	integer_multiply (a, b: like Current) is
			-- Multiply `a', `b' into `Current'.
		local
			i, j, carry, digit: INTEGER
			local_a, local_b: like Current
			l_digits: like digits
			l_b_count, l_a_count: INTEGER
			l_b_digits, l_a_digits: like digits
		do
				-- Initialization.
			-- grow (a.count + b.count + 2)
			from
				i := 0
				l_digits := digits
			until i >= count
			loop
#ifdef VE
				l_digits.put ('%U', 0) 
#else
				l_digits.put (0, i)
#endif
				i := i + 1
			end
				-- Multiplication.
			from
				if a.count > b.count then
					local_a := a
					local_b := b
				else
					local_a := b
					local_b := a
				end
				l_a_digits := local_a.digits
				l_b_digits := local_b.digits
				l_a_count := local_a.count
				l_b_count := local_b.count
				i := 0
			until
				i >= l_b_count -- local_b.count
			loop
#ifdef VE
				digit := l_b_digits.item (i).code --local_b.item (i)
#else
				digit := l_b_digits.item (i)
#endif
				carry := 0
				from
					j := 0
				until
					j >= l_a_count -- local_a.count
				loop
#ifdef VE
					carry := carry + digit * l_a_digits.item (j).code + l_digits.item (i + j).code
					l_digits.put (to_character (carry \\ 10), i + j)
#else
					carry := carry + digit * l_a_digits.item (j) + l_digits.item (i + j)
					l_digits.put ((carry \\ 10).to_integer_8, i + j)
#endif
					carry := carry // 10
					j := j + 1
				end
				if carry > 0 then
#ifdef VE
					l_digits.put (to_character (carry), i + j)
#else
					l_digits.put (carry.to_integer_8, i + j)
#endif
				end
				i := i + 1
			end
			set_count (i + j)
			strip_leading_zeroes
		end

	integer_quick_add_msd (other, digits_count: INTEGER) is
			-- Integer add of `other' (between 0 and 9) to `Current',
			-- restricted to `digits_count' most significant digits.
		local
			carry: INTEGER
			index: INTEGER
			digit: INTEGER
			to_add: INTEGER
			l_digits: like digits
			l_count: INTEGER
			shifting_delta: INTEGER
		do
			l_digits := digits
			l_count := count
			from
				carry := 0
				index := count - digits_count
				to_add := other
				shifting_delta := count + digits_count
			until
				index >= l_count
			loop
				if index > l_count - digits_count then
					to_add := 0
				end
#ifdef VE
				carry := carry + l_digits.item (index).code + to_add --item (index) + to_add
#else
				carry := carry + l_digits.item (index) + to_add
#endif
				digit := carry \\ 10
#ifdef VE
				l_digits.put (to_character (digit), index - count + digits_count) 
#else
				l_digits.put (digit.to_integer_8, index - count + digits_count)
#endif
				carry := carry // 10
				index := index + 1
			end
			if carry /= 0 then
#ifdef VE
				l_digits.put (to_character (carry), index - count + digits_count) --put (carry, index - count + digits_count)
#else
				l_digits.put (carry.to_integer_8, index - count + digits_count)
#endif
				set_count (digits_count + 1)
			else
				set_count (digits_count)
			end
		end

	integer_subtract (other: like Current) is
			-- Integer subtract of `other' to `Current',
			-- provided that result will not be negative.
		local
			borrow: INTEGER
			index: INTEGER
			l_count: INTEGER
			l_digits, l_other_digits: like digits
		do
			l_digits := digits
			l_other_digits := other.digits
			from
				borrow := 0
				index := 0
				l_count := other.msd_index + 1
			until
				index = l_count
			loop
#ifdef VE
				borrow := borrow + l_digits.item (index).code - l_other_digits.item (index).code --item (index) - other.item (index)
#else
				borrow := borrow + l_digits.item (index) - l_other_digits.item (index)
#endif
				if borrow < 0 then
#ifdef VE
					l_digits.put (to_character (10 + borrow), index) --put (10 + borrow, index)
#else
					l_digits.put ((10 + borrow).to_integer_8, index)
#endif
					borrow := -1
				else
#ifdef VE
					l_digits.put (to_character (borrow), index) -- put (borrow, index)
#else
					l_digits.put (borrow.to_integer_8, index)
#endif
					borrow := 0
				end
				index := index + 1
			end
			from
			until
				index = count
			loop
#ifdef VE
				borrow := borrow + l_digits.item (index).code --item (index)
#else
				borrow := borrow + l_digits.item (index)
#endif
				if borrow < 0 then
#ifdef VE				
					l_digits.put (to_character (10 + borrow), index) --put (10 + borrow, index)
#else
					l_digits.put ((10 + borrow).to_integer_8, index)
#endif
					borrow := -1
				else
#ifdef VE
					l_digits.put (to_character (borrow), index) -- put (borrow, index)
#else
					l_digits.put (borrow.to_integer_8, index)
#endif
					borrow := 0
				end
				index := index + 1
			end
			strip_leading_zeroes
		end

	integer_quick_subtract_msd (other, digits_count: INTEGER) is
			-- Integer subtract of `other' (between 0 and 9) to `Current'
			-- restricted to `digits_count' most significant digits.
		local
			borrow: INTEGER
			index, l_count, l_displacement: INTEGER
			to_subtract: INTEGER
			l_digits: like digits
		do
			from
				borrow := 0
				index := count - digits_count
				to_subtract := other
				l_count := count
				l_digits := digits
				l_displacement := count - digits_count
			until
				index = l_count
			loop
				if index > l_displacement then -- count - digits_count then
					to_subtract := 0
				end
#ifdef VE
				borrow := borrow + l_digits.item (index).code - to_subtract --item (index) - to_subtract
#else
				borrow := borrow + l_digits.item (index) - to_subtract
#endif
				if borrow < 0 then
#ifdef VE
					l_digits.put (to_character (10 + borrow), index - l_displacement) -- count + digits_count)
#else
					l_digits.put ((10 + borrow).to_integer_8, index - l_displacement) -- count + digits_count)
#endif
					borrow := -1
				else
#ifdef VE
					l_digits.put (to_character (borrow), index)
#else
					l_digits.put (borrow.to_integer_8, index)
#endif
					borrow := 0
				end
				index := index + 1
			end
			strip_leading_zeroes
		end

feature {MA_DECIMAL_COEFFICIENT_IMP} -- Implementation

#ifdef ISE || SE
	digits: SPECIAL [INTEGER_8]
			-- Array of digits (with the least significant first)

	SPECIAL_DIGITS_: KL_SPECIAL_ROUTINES [INTEGER_8] is
#else
	digits: SPECIAL [CHARACTER]
			-- Array of digits (with the least significant first)

	SPECIAL_DIGITS_: KL_SPECIAL_ROUTINES [CHARACTER] is
#endif
			-- Routines that ought to be in class SPECIAL
		once
			create Result
		ensure
			native_digits_array_routines_not_void: Result /= Void
		end

feature {MA_DECIMAL} -- Implementation

	set_count (a_count: INTEGER) is
			-- Set `count' to `a_count'.
		do
			count := a_count
		end

invariant

	digits_not_void: digits /= Void

end
