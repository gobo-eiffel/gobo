indexing

	description:

		"System clocks (precision to the millisecond)"

	remark:

		"With SmartEiffel under Windows the millisecond part may be stuck to zero."

	library: "Gobo Eiffel Kernel Library"
	copyright: "Copyright (c) 2001-2004, Eric Bezault and others"
	license: "Eiffel Forum License v2 (see forum.txt)"
	date: "$Date$"
	revision: "$Revision$"

class KL_SYSTEM_CLOCK

inherit

	KI_SYSTEM_CLOCK

#ifdef ISE
	C_DATE
		rename
			millisecond_now as millisecond
		undefine
			default_create
		end

#endif
#ifdef VE
	TIMESTAMP
		rename
			set_local_time as old_set_local_time
		export
			{NONE} all
		undefine
			out, is_equal
		end

#endif
create

	make

feature {NONE} -- Initialization

	make is
			-- Create a new system clock.
		do
#ifdef VE
			set (1970, 1, 1, 0, 0, 0.0)
#endif
		end

#ifndef VE
feature -- Access

	year: INTEGER
			-- Year

	month: INTEGER
			-- Month

	day: INTEGER
			-- Day

	hour: INTEGER
			-- Hour

	minute: INTEGER
			-- Minute

	second: INTEGER
			-- Second

#ifndef ISE
	millisecond: INTEGER
			-- Millisecond

#endif
#endif
feature -- Setting

	set_local_time is
			-- Set clock to current local time.
#ifdef SE
		local
			old_is_local: BOOLEAN
#endif
		do
#ifdef SE
			old_is_local := basic_time.time.is_local_time
			basic_time.time.set_local_time
			basic_time.update
			year := basic_time.time.year
			month := basic_time.time.month
			day := basic_time.time.day
			hour := basic_time.time.hour
			minute := basic_time.time.minute
			second := basic_time.time.second
			millisecond := basic_time.microsecond // 1000
			if not old_is_local then
				basic_time.time.set_universal_time
			end
#endif
#ifdef VE
			old_set_local_time
#endif
#ifdef ISE
			is_utc := False
			update
			year := year_now
			month := month_now
			day := day_now
			hour := hour_now
			minute := minute_now
			second := second_now
#endif
		end

	set_utc_time is
			-- Set clock to current UTC time.
#ifdef SE
		local
			old_is_local: BOOLEAN
#endif
		do
#ifdef SE
			old_is_local := basic_time.time.is_local_time
			basic_time.time.set_universal_time
			basic_time.update
			year := basic_time.time.year
			month := basic_time.time.month
			day := basic_time.time.day
			hour := basic_time.time.hour
			minute := basic_time.time.minute
			second := basic_time.time.second
			millisecond := basic_time.microsecond // 1000
			if old_is_local then
				basic_time.time.set_local_time
			end
#endif
#ifdef VE
			set_to_now
#endif
#ifdef ISE
			is_utc := True
			update
			year := year_now
			month := month_now
			day := day_now
			hour := hour_now
			minute := minute_now
			second := second_now
#endif
		end

#ifdef SE
feature {NONE} -- Implementation

	basic_time: MICROSECOND_TIME
			-- System time

#endif
end
